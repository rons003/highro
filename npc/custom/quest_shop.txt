//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
prontera,140,174,5	script	Aivy#1	879,{ callfunc "qshop";
OnInit:
waitingroom "Quest Shop",0;
end;
 }


// Script Core
//============================================================
-	script	quest_shop	-1,{ 
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Upper Headgear","Middle Headgear","Wings","Fusion Set","Weapons";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	// Upper Headgears
	Add(1,3677,1,0,0,744,350,1064,200,1009,300,7113,30,969,100,3100,10);
	Add(1,3016,1,0,0,3011,2,1059,300,7097,300,7020,30,1030,30,969,150,3100,20);
	Add(1,3010,1,0,0,2258,25,12085,2,7109,30,969,100,3100,10);
	Add(1,3011,1,0,0,2258,25,12075,2,968,50,969,100,3100,10);
	Add(1,3012,1,0,0,2258,25,12100,2,2282,30,969,100,3100,10);
	Add(1,3013,1,0,0,2258,25,12095,2,4039,100,4057,50,969,100,3100,10);
	Add(1,3014,1,0,0,2258,25,12080,2,7108,50,969,100,3100,10);
	Add(1,3015,1,0,0,2258,25,12090,2,982,15,969,100,3100,10);
	Add(1,5137,1,10000000,0,1064,150,7047,200,969,100,3100,5);
	Add(1,5138,1,10000000,0,1020,300,1034,300,4366,25,969,100,3100,10);
	Add(1,5013,1,5000000,0,754,25,753,25,752,25,751,10,750,10,743,25,742,25,741,25,5053,5,7020,20,7036,2,969,100,3100,10);
	Add(1,3663,1,10000000,0,7164,150,1064,100,7507,150,969,100,3100,10);
	//Add(1,30012,1,0,0,7086,5,5300,1,983,5);
	Add(1,3018,1,0,0,7088,3,12100,15,4317,5,4172,50,5383,1,976,15,969,100,3100,10);
	Add(1,3017,1,0,0,7089,3,5373,1,981,15,969,100,3100,10);
	Add(1,3662,1,0,0,7083,3,1030,100,923,500,4147,50,5160,25,969,100,3100,10);
	//Add(1,30014,1,0,0,2256,1,923,200,750,5,4147,20,947,100,5072,1,5066,1,1469,4,1130,4);
	//Add(1,5101,1,0,0,2295,1,7054,100,983,1);
	//Add(1,5102,1,0,0,7092,2,634,300,982,1);

	// Middle Headgears
	Add(2,3676,1,10000000,0,7301,400,7150,100,1009,250,969,100,3100,10);
	Add(2,3678,1,10000000,0,705,400,942,200,1009,100,969,100,3100,10);
	Add(2,5102,1,10000000,0,1059,300,909,300,4317,1,4172,30,969,100,3100,10);

	// Wings
	//Add(3,3003,1,5000000,0,1038,200,1039,200,983,2);
	//Add(3,30001,1,5000000,0,4174,1,2255,2,983,2);
	Add(3,3000,1,5000000,0,4054,1,2254,2,982,2,969,100,3100,10);
	Add(3,3001,1,15000000,0,995,25,749,25,7066,50,969,100,3100,10);
	Add(3,3007,1,15000000,0,994,80,954,80,7006,80,969,100,3100,10);
	Add(3,3003,1,15000000,0,1030,150,969,100,3100,10);
	Add(3,3005,1,15000000,0,7214,100,7063,100,6078,100,969,100,3100,10);
	Add(3,3660,1,15000000,0,7006,100,7030,100,995,100,969,100,3100,10);
	Add(3,3008,1,0,0,7101,100,924,150,7053,100,969,200,969,100,3100,10);
	Add(3,3009,1,0,0,7054,150,7063,20,982,10,969,200,969,100,3100,10);
	//Add(3,,1,0,0,7168,100,1058,70,983,10,969,200,969,100,3100,10);
	Add(3,3006,1,200000000,0,1058,500,3009,1,982,10,969,100,3100,10);

	// Fusion Set
	Add(4,30089,1,1000000,0,3670,2,976,3,969,100,3100,10);
	Add(4,30090,1,1000000,0,3672,2,978,3,969,100,3100,10);
	Add(4,30091,1,1000000,0,3673,2,982,3,969,100,3100,10);
	Add(4,30092,1,1000000,0,3671,2,981,3,969,100,3100,10);
	
	// Weapons
	Add(5,3500,1,1000000,0,1175,1,983,5,1030,30,2256,15,969,100,3100,10);
	Add(5,3501,1,1000000,0,1737,1,976,5,7108,30,968,30,969,100,3100,10);
	Add(5,3502,1,1000000,0,1614,1,978,5,7113,30,658,15,969,100,3100,10);
	Add(5,3503,1,1000000,0,1524,1,982,5,7020,30,5162,15,969,100,3100,10);
	Add(5,3504,1,1000000,0,1167,1,981,5,7109,30,5019,15,969,100,3100,10);



// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;


function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
